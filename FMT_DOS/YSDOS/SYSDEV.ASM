; Input
;   DS:SI  C-String may be just like "TOWNS_CD" or "DEV\TOWNS_CD"
; Return
;   CF=0  Found a CHARDEV  DS:SI is driver-name part ES:DI is pointer to SYSDEV
;   CF=1  Not a CHARDEV.  DS:SI and ES:DI no change.
; Destroys TEMPORARY_DIRENT.
; Destroys EAX, ECX
MATCH_CHARDEV_DSSI:
						PUSH	ES
						PUSh	DI
						PUSH	DS
						PUSH	SI

						CMP		DWORD PTR DS:[SI],05C564544h	; 'DEV\'
						JNE		MATCH_CHARDEV_DSSI_NOT_SLASHDEV
						ADD		SI,4	; Skip 'DEV\'

MATCH_CHARDEV_DSSI_NOT_SLASHDEV:
						PUSH	CS
						POP		ES
						MOV		DI,OFFSET TEMPORARY_DIRENT
						CALL	MAKE_11BYTE_FILENAME
						MOV		EAX,DWORD PTR CS:[TEMPORARY_DIRENT+8]
						AND		EAX,0ffffffh
						CMP		EAX,0202020h
						JNE		MATCH_CHARDEV_DSSI_NOT_FOUND

						MOV		EAX,DWORD PTR CS:[TEMPORARY_DIRENT]
						MOV		ECX,DWORD PTR CS:[TEMPORARY_DIRENT+4]
						; ES is already CS
						MOV		DI,OFFSET DEVHEAD   ; CS:DEVHEAD is the first device, a NUL device.
MATCH_CHARDEV_LOOP:
						CMP		DWORD PTR ES:[DI+SYSDEV_NAME],EAX
						JNE		MATCH_CHARDEV_LOOP_NEXT
						CMP		DWORD PTR ES:[DI+SYSDEV_NAME+4],ECX
						JNE		MATCH_CHARDEV_LOOP_NEXT
						TEST	WORD PTR ES:[DI+SYSDEV_DEVFLAGS],SYSDEV_DEVFLAG_IS_CHARDEV
						JNE		MATCH_CHARDEV_DSSI_FOUND
MATCH_CHARDEV_LOOP_NEXT:
						LES		DI,ES:[DI+SYSDEV_NEXT]
						CMP		DI,0ffffh
						JNE		MATCH_CHARDEV_LOOP

MATCH_CHARDEV_DSSI_NOT_FOUND:
						POP		SI
						POP		DS
						POP		DI
						POP		ES
						STC
						RET
MATCH_CHARDEV_DSSI_FOUND:
						; Don't touch DS:SI and ES:DI
						POP		EAX
						POP		EAX
						CLC
						RET
